JSON {
  Value
  = Object
    | Array
    | String
    | Number
    | True
    | False
    | Null

  Object
  = "{" "}" -- empty
    | "{" Pair ("," Pair)* "}" -- noEmpty

  Pair
  = String ":" Value

  Array
  = "[" "]" -- empty
    | "[" Value ("," Value)* "]" -- noEmpty

  String
  = "\"" doubleStringCharacter* "\""

  doubleStringCharacter (character)
  = ~("\"" | "\\") any -- nonEscaped
    | "\\" escapeSequence -- escaped

  escapeSequence
  = "\"" -- doubleQuote
    | "\\" -- reverseSolidus
    | "/" -- solidus
    | "b" -- backspace
    | "f" -- formfeed
    | "n" -- newline
    | "r" -- carriageReturn
    | "t" -- horizontalTab
    | "u" fourHexDigits -- codePoint

  fourHexDigits = hexDigit hexDigit hexDigit hexDigit

  Number
  = decimal exponent -- withExponent
    | decimal -- withoutExponent

  decimal
  = wholeNumber "." digit+ -- withFract
    | wholeNumber -- withoutFract

  wholeNumber
  = "-" unsignedWholeNumber -- negative
    | unsignedWholeNumber -- nonNegative

  unsignedWholeNumber
  = "0" -- zero
    | nonZeroDigit digit* -- nonZero

  nonZeroDigit = "1".."9"

  exponent
  = exponentMark ("+"|"-") digit+ -- signed
    | exponentMark digit+ -- unsigned

  exponentMark = "e" | "E"

  True = "true"
  False = "false"
  Null = "null"
}
